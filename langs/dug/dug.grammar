todo Add swarm tests for top scenarios, including the scalar at root level scenario.
todo Create a new language, similar to this, except using pattern matching instead of prefix notation.
dugNode
 root
 description A demonstration language that compiles to JSON.
 inScope valueNode
 catchAllNodeType errorNode
 javascript
  compile() {
    const res = super.compile()
    return JSON.stringify(JSON.parse(res), null, 2)
  }
anyFirstCell
typeNameCell
 highlightScope keyword
stringCell
 highlightScope string
booleanCell
 enum true false
 highlightScope constant.numeric
numberCell
 highlightScope constant.numeric
valueNode
 firstCellType typeNameCell
nullNode
 compiler
  stringTemplate null
 extends valueNode
 match null
numberNode
 match number
 extends valueNode
 cells numberCell
 compiler
  stringTemplate {numberCell}
stringNode
 match string
 catchAllCellType stringCell
 compiler
  stringTemplate "{stringCell}"
 extends valueNode
booleanNode
 match boolean
 extends valueNode
 cells booleanCell
 compiler
  stringTemplate {booleanCell}
memberNode
 firstCellType stringCell
 inScope valueNode
 compiler
  stringTemplate "{stringCell}" :
objectNode
 match object
 catchAllNodeType memberNode
 extends valueNode
 compiler
  stringTemplate  
  joinChildrenWith , 
  openChildren {
  closeChildren }
arrayNode
 match array
 extends valueNode
 inScope valueNode
 compiler
  stringTemplate  
  joinChildrenWith , 
  openChildren [
  closeChildren ]
errorNode
 baseNodeType errorNode