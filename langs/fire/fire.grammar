todo Explore best ways to add polymorphism
todo Explore non-firstCell node type detection
fireNode
 root
 description A useless Tree Language that compiles to Javascript for testing Tree Notation features.
 compilesTo js
 inScope hashbangNode abstractTerminalNode abstractNonTerminalNode
 catchAllNodeType errorNode
 javascript
  async execute() { return this.executeSync() }
  executeSync() {
    let outputLines = []
    const _originalConsoleLog = console.log
    const tempConsoleLog = (...params) => outputLines.push(params)
    console.log = tempConsoleLog
    const compiled = this.compile("js")
    eval(compiled)
    console.log = _originalConsoleLog
    console.log(outputLines.join("\n"))
    return outputLines
  }
anyFirstCell
anyCell
booleanCell
 regex (false|true)
filepathCell
identifierCell
 regex [$A-Za-z_][0-9a-zA-Z_$]*
 highlightScope variable
numberCell
 regex \-?[0-9]*\.?[0-9]*
 highlightScope constant.numeric
numberIdentifierCell
 extends identifierCell
hashBangCell
 highlightScope comment
hashBangKeywordCell
 highlightScope comment
stringCell
 highlightScope string
booleanIdentifierCell
 extends identifierCell
functionIdentifierCell
 extends identifierCell
identifiersCell
 extends identifierCell
instanceIdentifierCell
 extends identifierCell
methodIdentifierCell
 extends identifierCell
resultIdentifierCell
 extends identifierCell
keywordCell
 highlightScope keyword
stringIdentifierCell
 extends identifierCell
stringCellsCell
 extends stringCell
abstractNonTerminalNode
 inScope abstractTerminalNode abstractNonTerminalNode
 firstCellType keywordCell
 abstract
abstractTerminalNode
 firstCellType keywordCell
 abstract
abstractAssignmentNode
 extends abstractTerminalNode
 abstract
abstractArithmeticNode
 cells identifierCell
 catchAllCellType anyCell
 compiler
  stringTemplate const {identifierCell} = {anyCell}
 frequency .2
 extends abstractAssignmentNode
 abstract
abstractJsblockNode
 compiler
  openChildren  {
  closeChildren }
 extends abstractNonTerminalNode
 abstract
abstractBooleanOperatorNode
 description Runs a boolean test and saves result.
 extends abstractAssignmentNode
 abstract
hashbangNode
 match #!
 description Standard bash hashbang line.
 catchAllCellType hashBangCell
 firstCellType hashBangKeywordCell
 compiler
  stringTemplate // #! {hashBangCell}
errorNode
 baseNodeType errorNode
 compiler
  stringTemplate // error
divideNode
 description Divide Numbers
 compiler
  catchAllCellDelimiter  / 
 extends abstractArithmeticNode
moduloNode
 description Modulo Numbers
 compiler
  catchAllCellDelimiter %
 extends abstractArithmeticNode
multiplyNode
 description Multiply Numbers
 compiler
  catchAllCellDelimiter  * 
 extends abstractArithmeticNode
substractNode
 description Subtract Numbers
 compiler
  catchAllCellDelimiter  - 
 extends abstractArithmeticNode
addNode
 description Add numbers and store result
 compiler
  catchAllCellDelimiter  + 
 extends abstractArithmeticNode
sumNode
 description Add numbers and store result
 cells numberIdentifierCell
 catchAllCellType numberCell
 compiler
  stringTemplate const {numberIdentifierCell} = [{numberCell}].reduce((sum, num) => sum + num)
  catchAllCellDelimiter , 
 frequency .1
 extends abstractAssignmentNode
booleanNode
 description Boolean Assignment
 cells booleanIdentifierCell booleanCell
 compiler
  stringTemplate const {booleanIdentifierCell} = {booleanCell}
 extends abstractAssignmentNode
callFunctionAndSetNode
 description Function Call
 frequency .5
 cells resultIdentifierCell functionIdentifierCell
 catchAllCellType anyCell
 compiler
  stringTemplate const {resultIdentifierCell} = {functionIdentifierCell}({anyCell})
  catchAllCellDelimiter , 
 extends abstractAssignmentNode
callMethodAndSetNode
 description Method Call
 frequency .5
 cells resultIdentifierCell instanceIdentifierCell methodIdentifierCell
 catchAllCellType anyCell
 compiler
  stringTemplate const {resultIdentifierCell} = {instanceIdentifierCell}.{methodIdentifierCell}({anyCell})
  catchAllCellDelimiter , 
 extends abstractAssignmentNode
joinNode
 description Join strings to form new string
 cells identifierCell
 catchAllCellType identifiersCell
 compiler
  stringTemplate const {identifierCell} = [{identifiersCell}].join("")
  catchAllCellDelimiter , 
 frequency .2
 extends abstractAssignmentNode
mutableNumberNode
 description Mutable Number Assignment
 cells identifierCell numberCell
 compiler
  stringTemplate let {identifierCell} = {numberCell}
 extends abstractAssignmentNode
numberNode
 description Number Assignment
 cells identifierCell numberCell
 compiler
  stringTemplate const {identifierCell} = {numberCell}
 frequency .3
 extends abstractAssignmentNode
numbersNode
 description Number Array Assignment
 cells identifierCell
 catchAllCellType numberCell
 frequency .4
 compiler
  stringTemplate const {identifierCell} = [{numberCell}]
  catchAllCellDelimiter , 
 extends abstractAssignmentNode
stringNode
 description String Assignment
 cells stringIdentifierCell
 catchAllCellType anyCell
 compiler
  stringTemplate const {stringIdentifierCell} = "{anyCell}"
 frequency .2
 extends abstractAssignmentNode
leftNumberCell
 extends numberCell
leftAnyCell
 extends anyCell
greaterThanNode
 description Greater than test
 cells identifierCell leftNumberCell numberCell
 compiler
  stringTemplate const {identifierCell} = {leftNumberCell} > {numberCell}
 frequency .1
 extends abstractBooleanOperatorNode
greaterThanOrEqualNode
 description Greater than or equal to test
 cells identifierCell leftNumberCell numberCell
 compiler
  stringTemplate const {identifierCell} = {leftNumberCell} >= {numberCell}
 frequency .1
 extends abstractBooleanOperatorNode
lessThanNode
 description Less than test
 cells identifierCell leftAnyCell anyCell
 compiler
  stringTemplate const {identifierCell} = {leftAnyCell} < {anyCell}
 frequency .1
 extends abstractBooleanOperatorNode
lessThanOrEqualNode
 description Less than or equal to test
 cells identifierCell leftAnyCell anyCell
 compiler
  stringTemplate const {identifierCell} = {leftAnyCell} <= {anyCell}
 frequency .1
 extends abstractBooleanOperatorNode
blockNode
 description block of code
 frequency .2
 compiler
  stringTemplate /* {identifierCell} */
 extends abstractJsblockNode
functionNode
 match function
 description Function Assignment
 cells functionIdentifierCell
 catchAllCellType anyCell
 compiler
  stringTemplate const {functionIdentifierCell} = ({anyCell}) =>
  catchAllCellDelimiter , 
 frequency .1
 extends abstractJsblockNode
ifNode
 match if
 description If tile
 cells identifierCell
 frequency .2
 compiler
  stringTemplate if ({identifierCell})
 extends abstractJsblockNode
whileNode
 match while
 description While tile
 cells identifierCell
 frequency .1
 compiler
  stringTemplate while ({identifierCell})
 extends abstractJsblockNode
callFunctionNode
 description Function call ignore result.
 frequency .1
 cells functionIdentifierCell
 catchAllCellType anyCell
 compiler
  stringTemplate {functionIdentifierCell}({anyCell})
  catchAllCellDelimiter , 
 extends abstractTerminalNode
decrementNode
 description Decrement
 cells numberIdentifierCell
 compiler
  stringTemplate {numberIdentifierCell}--
 frequency .1
 extends abstractTerminalNode
dumpIdentifierNode
 description Dump variable(s) to console
 catchAllCellType identifierCell
 compiler
  stringTemplate console.log({identifierCell})
  catchAllCellDelimiter , 
 frequency .5
 extends abstractTerminalNode
exportNode
 match export
 description Export This
 cells identifierCell
 compiler
  stringTemplate module.exports = {identifierCell}
 frequency .1
 extends abstractTerminalNode
incrementNode
 description Increment
 frequency .3
 cells numberIdentifierCell
 compiler
  stringTemplate {numberIdentifierCell}++
 extends abstractTerminalNode
printNumberNode
 extends abstractTerminalNode
 catchAllCellType numberIdentifierCell
 compiler
  stringTemplate console.log({numberIdentifierCell})
printStringNode
 todo Allow printing of multiline strings
 extends abstractTerminalNode
 catchAllCellType stringCellsCell
 compiler
  stringTemplate console.log("{stringCells}")
requireNode
 match require
 description Require Something
 cells identifierCell filepathCell
 compiler
  stringTemplate const {identifierCell} = require("{filepathCell}")
 frequency .1
 extends abstractTerminalNode
returnNode
 match return
 cells anyCell
 compiler
  stringTemplate return {anyCell}
 frequency .1
 extends abstractTerminalNode